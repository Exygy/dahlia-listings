ShortFormApplicationService = ($localStorage, $translate, $http) ->
  Service = {}
  Service.applicationDefaults =
    applicant:
      primary_language: "English"
      phone_number: null
      home_address: { address1: null, city: null, state: null, zip: null }
      mailing_address: { address1: null, city: null, state: null, zip: null }
    alternateContact:
      primary_language: "English"
    householdMembers: []
    preferences:
      live_in_sf: null
      work_in_sf: null
    householdIncome: { income_total: 0, income_timeframe: 'per_year' }
    completedSections:
      Intro: false
      You: false
      Household: false
      Status: false
      Income: false

  Service.current_id = 0

  Service.resetUserData = () ->
    Service.application = angular.copy(Service.applicationDefaults)
    Service.applicant = Service.application.applicant
    Service.alternateContact = Service.application.alternateContact
    Service.householdMember = {}
    Service.householdMembers = Service.application.householdMembers

  # even though this looks commented out, it still has an effect in ERB land
  # <% if Rails.env.development? %> # -------------
  $localStorage.application ?= Service.applicationDefaults
  Service.application = $localStorage.application
  Service.applicant = Service.application.applicant
  Service.preferences = Service.application.preferences
  Service.alternateContact = Service.application.alternateContact
  Service.householdMember = {}
  Service.householdMembers = Service.application.householdMembers
  # <% else %> # -------------
  # initialize the user application data
  Service.resetUserData()
  # <% end %> # -------------

  Service.completeSection = (section) ->
    Service.application.completedSections[section] = true

  Service.userCanAccessSection = (name, state = null) ->
    # user can't access later sections yet when on the welcome page
    return false if state and Service.isWelcomePage(state)
    Service.application.completedSections ?= Service.applicationDefaults.completedSections
    completed = Service.application.completedSections
    switch name
      when 'You'
        true
      when 'Household'
        completed.Intro &&
        completed.You
      when 'Status'
        completed.Intro &&
        completed.You &&
        completed.Household
      when 'Income'
        completed.Intro &&
        completed.You &&
        completed.Household &&
        completed.Status
      when 'Review'
        completed.Intro &&
        completed.You &&
        completed.Household &&
        completed.Status &&
        completed.Income
      else
        false

  Service.copyHomeToMailingAddress = () ->
    angular.copy(Service.applicant.home_address, Service.applicant.mailing_address)

  Service.validMailingAddress = () ->
    !! (Service.applicant.mailing_address.address1 &&
        Service.applicant.mailing_address.city &&
        Service.applicant.mailing_address.state &&
        Service.applicant.mailing_address.zip)

  Service.missingPrimaryContactInfo = () ->
    missingInfo = []
    if !Service.applicant.phone_number
      missingInfo.push("Phone")
    if !Service.applicant.email
      missingInfo.push("Email")
    if !Service.validMailingAddress()
      missingInfo.push("Address")
    return missingInfo

  Service._nextId = ->
    if Service.householdMembers.length > 0
      max_id = _.maxBy(Service.householdMembers, 'id').id
    else
      max_id = Service.current_id
    Service.current_id = max_id + 1

  Service.addHouseholdMember = (householdMember) ->
    if !householdMember.id
      householdMember.id = Service._nextId()
      Service.householdMembers.push(angular.copy(householdMember))
    Service.householdMember = {}

  Service.resetHouseholdmember = () ->
    Service.householdMember = {}

  Service.getHouseholdMember = (id) ->
    Service.householdMember = _.find(Service.householdMembers, {id: parseInt(id)})

  Service.cancelHouseholdMember = ->
    householdMembers = Service.householdMembers.filter (m) ->
      (m != Service.householdMember && m.id != Service.householdMember.id)
    # clear out preferences where this householdMember was selected
    full_name = "#{Service.householdMember.first_name} #{Service.householdMember.last_name}"
    # search through all xxx_household_member items in preferences matching the full name
    _.each Service.application.preferences, (v,k) ->
      if k.indexOf('_household_member') > 0 and v == full_name
        preference = k.split('_household_member')[0]
        Service.application.preferences[preference] = false
        Service.application.preferences[k] = null
        if Service.application.preferences["#{preference}_proof_option"]
          Service.application.preferences["#{preference}_proof_option"] = null
        if Service.application.preferences["#{preference}_proof_file"]
          Service.application.preferences["#{preference}_proof_file"] = null

    # persist the changes to Service.householdMembers / $localStorage
    Service.householdMember = {}
    angular.copy(householdMembers, Service.householdMembers)

  Service.checkHouseholdEligiblity = (listing) ->
    params =
      listing_id: listing.Id,
      eligibility:
        householdsize: Service.application.householdMembers.length + 1
        incomelevel: Service._calculateHouseholdIncome()
    $http.post("/api/v1/validate-household", params).success((data, status, headers, config) ->
      data
    ).error( (data, status, headers, config) ->
      return
    )

  Service._calculateHouseholdIncome = () ->
    income = Service.application.householdIncome
    if income.income_timeframe == 'per_year'
      income.income_total
    else if income.income_timeframe == 'per_month'
      income.income_total * 12

  Service.resetGenderOptions = (user, option) ->
    toggle = !user.gender[option]
    user.gender = {}
    user.gender_not_listed = null
    user.gender[option] = toggle

  ### Proof of Preferences ###

  Service.refreshLiveWorkPreferences = () ->
    Service._updatePreference("live_in_sf", Service.liveInSfMembers())
    Service._updatePreference("work_in_sf", Service.workInSfMembers())

  Service.liveInSfMembers = () ->
    applicantLivesInSf = _.lowerCase(Service.applicant.home_address.city) == 'san francisco'
    liveInSfMembers = Service.application.householdMembers.filter (member) ->
      if member.same_address == "No"
        _.lowerCase(member.home_address.city) == 'san francisco'
      else
        member.same_address == "Yes" && applicantLivesInSf
    liveInSfMembers.push(Service.applicant) if applicantLivesInSf
    return liveInSfMembers

  Service.workInSfMembers = () ->
    workInSfMembers = Service.application.householdMembers.filter (member) ->
      member.work_in_sf == "Yes"
    applicantWorksInSf = Service.applicant.work_in_sf == "Yes"
    workInSfMembers.push(Service.applicant) if applicantWorksInSf
    return workInSfMembers

  Service._updatePreference = (preference, eligibleMembers) ->
    # only need to manually update if the preference hasn't been set yet
    return unless Service.preferences[preference] == null
    if eligibleMembers.length > 0
      Service.preferences[preference] = true
    else
      Service.preferences[preference] = false

  Service.onExit = (e) ->
    e.returnValue = $translate.instant('T.ARE_YOU_SURE_YOU_WANT_TO_LEAVE')

  Service.isWelcomePage = (state) ->
    !!state.name.match(/short-form-welcome\./)

  Service.isShortFormPage = (state) ->
    !!state.name.match(/short-form-application\./)

  Service.isJumpingAhead = (toState, fromState) ->
    return false unless toState && fromState
    # they're jumping ahead if they're starting from '' (meaning new browser window)
    # and they're on a short form section ahead of "name" (1st page)
    !! (Service.isShortFormPage(toState) &&
      fromState.name == '' &&
      toState.name != 'dahlia.short-form-application.name')

  Service.isLeavingShortForm = (toState, fromState) ->
    onShortForm = Service.isShortFormPage(fromState)
    # leaving means we're not going to another short form page or to create account
    leaving = !(Service.isShortFormPage(toState) || toState.name.match(/create-account/))
    onShortForm && leaving

  return Service

############################################################################################
######################################## CONFIG ############################################
############################################################################################

ShortFormApplicationService.$inject = ['$localStorage', '$translate', '$http']

angular
  .module('dahlia.services')
  .service('ShortFormApplicationService', ShortFormApplicationService)
