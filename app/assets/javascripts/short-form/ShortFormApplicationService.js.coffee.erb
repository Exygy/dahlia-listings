ShortFormApplicationService = ($localStorage, $translate) ->
  Service = {}
  Service.applicationDefaults = {
    applicant: {
      primary_language: "English",
      phone_number: null,
      home_address: { address1: null, city: null, state: null, zip: null },
      mailing_address: { address1: null, city: null, state: null, zip: null },
      preferences: {live_in_sf: false, work_in_sf: false}
    }
    alternateContact: {
      primary_language: "English"
    }
    householdMembers: []
    preferences: {}
    completedSections: {
      Intro: false,
      You: false,
      Household: false,
      Status: false,
      Income: false
    }
  }
  Service.current_id = 0

  Service.resetUserData = () ->
    Service.application = angular.copy(Service.applicationDefaults)
    Service.applicant = Service.application.applicant
    Service.alternateContact = Service.application.alternateContact
    Service.householdMember = {}
    Service.householdMembers = Service.application.householdMembers

  <% if Rails.env.development? %>
  $localStorage.application ?= Service.applicationDefaults
  Service.application = $localStorage.application
  Service.applicant = Service.application.applicant
  Service.alternateContact = Service.application.alternateContact
  Service.householdMember = {}
  Service.householdMembers = Service.application.householdMembers
  <% else %>
  # initialize the user application data
  Service.resetUserData()
  <% end %>

  Service.completeSection = (section) ->
    Service.application.completedSections[section] = true

  Service.userCanAccessSection = (section) ->
    Service.application.completedSections ?= Service.applicationDefaults.completedSections
    completed = Service.application.completedSections
    switch section.name
      when 'You'
        completed.Intro
      when 'Household'
        completed.Intro &&
        completed.You
      when 'Status'
        completed.Intro &&
        completed.You &&
        completed.Household
      when 'Income'
        completed.Intro &&
        completed.You &&
        completed.Household &&
        completed.Status
      when 'Review'
        completed.Intro &&
        completed.You &&
        completed.Household &&
        completed.Status &&
        completed.Income
      else
        false

  Service.copyHomeToMailingAddress = () ->
    angular.copy(Service.applicant.home_address, Service.applicant.mailing_address)

  Service.validMailingAddress = () ->
    !! (Service.applicant.mailing_address.address1 &&
        Service.applicant.mailing_address.city &&
        Service.applicant.mailing_address.state &&
        Service.applicant.mailing_address.zip)

  Service.missingPrimaryContactInfo = () ->
    missingInfo = []
    if !Service.applicant.phone_number
      missingInfo.push("Phone")
    if !Service.applicant.email
      missingInfo.push("Email")
    if !Service.validMailingAddress()
      missingInfo.push("Address")
    return missingInfo

  Service._nextId = ->
    if Service.householdMembers.length > 0
      max_id = _.maxBy(Service.householdMembers, 'id').id
    else
      max_id = Service.current_id
    Service.current_id = max_id + 1

  Service.addHouseholdMember = (householdMember) ->
    if !householdMember.id
      householdMember.id = Service._nextId()
      Service.householdMembers.push(angular.copy(householdMember))
    Service.householdMember = {}

  Service.getHouseholdMember = (id) ->
    Service.householdMember = _.find(Service.householdMembers, {id: parseInt(id)})

  Service.cancelHouseholdMember = ->
    householdMembers = Service.householdMembers.filter (m) ->
      (m != Service.householdMember && m.id != Service.householdMember.id)
    # clear out preferences where this householdMember was selected
    full_name = "#{Service.householdMember.first_name} #{Service.householdMember.last_name}"
    # search through all xxx_household_member items in preferences matching the full name
    _.each Service.application.preferences, (v,k) ->
      if k.indexOf('_household_member') > 0 and v == full_name
        preference = k.split('_household_member')[0]
        Service.application.preferences[preference] = false
        Service.application.preferences[k] = null
        if Service.application.preferences["#{preference}_proof_option"]
          Service.application.preferences["#{preference}_proof_option"] = null
        if Service.application.preferences["#{preference}_proof_file"]
          Service.application.preferences["#{preference}_proof_file"] = null

    # persist the changes to Service.householdMembers / $localStorage
    Service.householdMember = {}
    angular.copy(householdMembers, Service.householdMembers)

  Service.resetGenderOptions = (user, option) ->
    toggle = !user.gender[option]
    user.gender = {}
    user.gender_not_listed = null
    user.gender[option] = toggle

  Service.livesInSf = () ->
    membersLiveInSf = Service.application.householdMembers.filter (member) ->
      if member.home_address
        _.lowerCase(member.home_address.city) == 'san francisco'
    applicantLivesInSf = _.lowerCase(Service.applicant.home_address.city) == 'san francisco'
    if membersLiveInSf.length > 0 || applicantLivesInSf
      Service.applicant.preferences.live_in_sf = true
    else
      Service.applicant.preferences.live_in_sf = false

  Service.worksInSf = () ->
    membersWorkInSf = Service.application.householdMembers.filter (member) ->
      member.work_in_sf == 'Yes'
    if (membersWorkInSf.length > 0) || (Service.applicant.work_in_sf == "Yes")
      Service.applicant.preferences.work_in_sf = true
    else
      Service.applicant.preferences.work_in_sf = false

  Service.onExit = (e) ->
    e.returnValue = $translate.instant('T.ARE_YOU_SURE_YOU_WANT_TO_LEAVE')

  Service.isLeavingShortForm = (toState, fromState) ->
    return false if fromState.name.match(/short-form-application\.(intro|overview)/)
    onShortForm = fromState.name.indexOf('short-form-application') >= 0
    leaving = toState.name.indexOf('short-form-application') == -1 &&
          toState.name.indexOf('create-account') == -1
    onShortForm && leaving

  return Service

############################################################################################
######################################## CONFIG ############################################
############################################################################################

ShortFormApplicationService.$inject = ['$localStorage', '$translate']

angular
  .module('dahlia.services')
  .service('ShortFormApplicationService', ShortFormApplicationService)
